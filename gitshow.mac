// git show

disabledraw;

// メニュー用にログを取得する
$$git = "git.exe log --date=iso --pretty=format:\"[%ad] %h %an : %s\" ";
$$cmd = $$git + filename2;
$$dir = directory2 + "\\";
##current_handle = hidemaruhandle(0);
$$log = getenv("TMP") + "\\" + basename2 + "log" + "." + filetype;
runex $$cmd,0,0,"",2,$$log,0,"",2,$$dir,0,0,encode;
if (existfile($$log, 2) == 0) {
  endmacro;
}

// 隠しタブにログを表示する
openfile "/h " + $$log;
##tmp_handle = hidemaruhandle(0);
##start = 1;
##count = ##start;
$$menu[0] = "■■■表示するコミットを選択してください";
while (##count <= 20 + ##start) {
  if (result == false){
    break;
  }
  beginclipboardread;
  beginlinesel 0;
  copy;
  $$menu[##count] = getclipboard;
  down;
  ##count = ##count + 1;
}
enabledraw;
setactivehidemaru ##current_handle;
closehidemaru ##tmp_handle;

// メニューを表示する
menuarray $$menu,##count;
##idx = result;

$$commit_hash = "";
if (##idx > ##start) {
  // 選択されたメニューからコミットハッシュを取り出す
  $$commit_hash = midstr($$menu[##idx - 1], 28, 7);
}
if (strlen($$commit_hash) != 7) {
  endmacro;
}
$$file = getenv("TMP") + "\\" + basename2 + $$commit_hash + "." + filetype;

// コミットハッシュの内容をファイルにする
$$git = "git.exe show ";
$$cmd = $$git + $$commit_hash + ":" + basename2 + " > " + $$file;
run $$cmd;
if (result == false) {
  message "gitのコマンド実行時にエラーが発生しました。\n" + $$cmd;
  endmacro;
}

// ファイルにした内容を表示する
openfile "/r " + $$file;
endmacro;
