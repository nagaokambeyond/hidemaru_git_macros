// git showとdiff

$$tool = "C:\\Program Files\\WinMerge\\WinMergeU.exe";

if (existfile($$tool) == false) {
  message "ツールが存在しません。\n" + $$tool;
  endmacro;
}

beginclipboardread;
$$commit_hash = getclipboard;

if (strlen($$commit_hash) != 40) {
  message "コミットハッシュがクリップボードにありません。";
  endmacro;
}
$$file = getenv("TMP") + "\\" + basename + $$commit_hash + "." + filetype;

if (existfile($$file) == false) {
  // コミットハッシュの内容をファイルにする
  $$git = "git.exe show ";
  $$cmd = $$git + $$commit_hash + ":" + basename + " > " + $$file;
  run $$cmd;
  if (result == false) {
    message "gitのコマンド実行時にエラーが発生しました。\n" + $$cmd;
    endmacro;
  }
}

// toolの実行する
$$cmd = $$tool + " " + filename2 + " " + $$file;
run $$cmd;
if (result == false) {
  message "比較ツール実行時にエラーが発生しました。\n" + $$cmd;
  endmacro;
}

endmacro;
