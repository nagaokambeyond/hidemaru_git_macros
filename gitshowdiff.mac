// git showとdiff

$$tool = "C:\\Program Files\\WinMerge\\WinMergeU.exe";

if (existfile($$tool) == false) {
  message "ツールが存在しません。\n" + $$tool;
  endmacro;
}

disabledraw;

// メニュー用にログを取得する
$$git = "git.exe log --date=iso --pretty=format:\"[%ad] %H %an : %s\" ";
$$cmd = $$git + filename2;
$$dir = directory2 + "\\";
##main_handle = hidemaruhandle(0);
$$log = getenv("TMP") + "\\" + basename + "log" + "." + filetype;
runex $$cmd,0,0,"",2,$$log,0,"",2,$$dir,0,0,encode;

// 隠しタブにメニュー用にログを表示する
openfile "/h " + $$log;
##tmp_handle = hidemaruhandle(0);
##count = 0;
while (##count <= 10) {
  if (result == false){
    break;
  }
  beginclipboardread;
  beginlinesel 0;
  copy;
  $$menu[##count] = getclipboard;
  down;
  ##count = ##count + 1;
}
enabledraw;
setactivehidemaru ##main_handle;
closehidemaru ##tmp_handle;

// メニューを表示する
menuarray $$menu,##count;

$$commit_hash = "";
##idx = result;
if (##idx > 0) {
  // 選択されたメニューからコミットハッシュを取り出す
  $$commit_hash = midstr($$menu[##idx - 1], 28, 40);
}
if (strlen($$commit_hash) != 40) {
  endmacro;
}

$$file = getenv("TMP") + "\\" + basename + $$commit_hash + "." + filetype;

if (existfile($$file) == false) {
  // コミットハッシュの内容をファイルにする
  $$git = "git.exe show ";
  $$cmd = $$git + $$commit_hash + ":" + basename + " > " + $$file;
  run $$cmd;
  if (result == false) {
    message "gitのコマンド実行時にエラーが発生しました。\n" + $$cmd;
    endmacro;
  }
}

// toolの実行する
$$cmd = $$tool + " " + filename2 + " " + $$file;
run $$cmd;
if (result == false) {
  message "比較ツール実行時にエラーが発生しました。\n" + $$cmd;
  endmacro;
}

endmacro;
