// git showとdiff

$$tool = "C:\\Program Files\\WinMerge\\WinMergeU.exe";

if (existfile($$tool) == false) {
  message "比較ツールが存在しません。\n" + $$tool;
  endmacro;
}

##log_max_count = 60;
##max_width = 70;

disabledraw;
disablehistory 0x0041;

// メニュー用にログを取得する
$$git = "git.exe log -" + str(##log_max_count) + " --no-merges --date=short --pretty=format:\"[%ad] %h %an : %s\" " ;
$$cmd = $$git + filename2;
$$dir = directory2 + "\\";
##current_handle = hidemaruhandle(0);
$$log = getenv("TMP") + "\\" + basename2 + "log" + "." + filetype;
runex $$cmd,0,0,"",2,$$log,0,"",2,$$dir,0,0,encode;
if (existfile($$log, 2) == 0) {
  endmacro;
}
$$comment = leftstr("■比較するコミットを選択してください■" + basename2,##max_width);

// 隠しタブにログを表示する
openfile "/h " + $$log;
##tmp_handle = hidemaruhandle(0);
##start_row_index = 2;
##count = ##start_row_index;
$$menu[0] = $$comment;
$$menu[1] = "\x01";
while (##count <= ##log_max_count + ##start_row_index) {
  if (result == false){
    break;
  }
  beginclipboardread;
  beginlinesel 0;
  copy;
  $$menu[##count] = leftstr(getclipboard,##max_width);
  down;
  ##count = ##count + 1;
}
enabledraw;
setactivehidemaru ##current_handle;
closehidemaru ##tmp_handle;

// メニューを表示する
menuarray $$menu,##count;
##idx = result;

$$commit_hash = "";
if (##idx > ##start_row_index) {
  // 選択されたメニューからコミットハッシュを取り出す
  $$commit_hash = midstr($$menu[##idx - 1], 13, 7);
}
if (strlen($$commit_hash) != 7) {
  endmacro;
}

// コミットハッシュの内容をファイルにする
$$file = getenv("TMP") + "\\" + basename2 + $$commit_hash + "." + filetype;
$$git = "git.exe show ";
$$cmd = $$git + $$commit_hash + ":" + basename2 + " > " + $$file;
run $$cmd;
if (result == false) {
  message "gitのコマンド実行時にエラーが発生しました。\n" + $$cmd;
  endmacro;
}

// 比較ツールを実行する
$$cmd = $$tool + " " + $$file + " " + filename2;
run $$cmd;
if (result == false) {
  message "比較ツール実行時にエラーが発生しました。\n" + $$cmd;
  endmacro;
}

endmacro;
